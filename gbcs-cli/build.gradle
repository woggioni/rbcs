plugins {
    id 'java-library'
    alias catalog.plugins.kotlin.jvm
    alias catalog.plugins.envelope
    alias catalog.plugins.sambal
    alias catalog.plugins.graalvm.native.image
    alias catalog.plugins.graalvm.jlink
    alias catalog.plugins.jpms.check
    id 'maven-publish'
}

import net.woggioni.gradle.envelope.EnvelopeJarTask
import net.woggioni.gradle.graalvm.NativeImageConfigurationTask
import net.woggioni.gradle.graalvm.NativeImagePlugin
import net.woggioni.gradle.graalvm.NativeImageTask
import net.woggioni.gradle.graalvm.JlinkPlugin
import net.woggioni.gradle.graalvm.JlinkTask

Property<String> mainClassName = objects.property(String.class)
mainClassName.set('net.woggioni.gbcs.cli.GradleBuildCacheServerCli')

tasks.named(JavaPlugin.COMPILE_JAVA_TASK_NAME, JavaCompile) {
    options.javaModuleMainClass = mainClassName
}

configurations {
    release {
        transitive = false
        canBeConsumed = true
        canBeResolved = true
        visible = true
    }
}

envelopeJar {
    mainModule = 'net.woggioni.gbcs.cli'
    mainClass = mainClassName

    extraClasspath = ["plugins"]
}

dependencies {
    implementation catalog.jwo
    implementation catalog.slf4j.api
    implementation catalog.netty.codec.http
    implementation catalog.picocli

    implementation project(':gbcs-client')
    implementation project(':gbcs-server')

//    runtimeOnly catalog.slf4j.jdk14
    runtimeOnly catalog.logback.classic
}

Provider<EnvelopeJarTask> envelopeJarTaskProvider = tasks.named('envelopeJar', EnvelopeJarTask.class) {
//    systemProperties['java.util.logging.config.class'] = 'net.woggioni.gbcs.LoggingConfig'
//    systemProperties['log.config.source'] = 'logging.properties'
    systemProperties['logback.configurationFile'] = 'classpath:net/woggioni/gbcs/cli/logback.xml'
}

tasks.named(NativeImagePlugin.CONFIGURE_NATIVE_IMAGE_TASK_NAME, NativeImageConfigurationTask) {
    mainClass = mainClassName
}

tasks.named(NativeImagePlugin.NATIVE_IMAGE_TASK_NAME, NativeImageTask) {
    mainClass = mainClassName
    useMusl = true
    buildStaticImage = true
}

tasks.named(JlinkPlugin.JLINK_TASK_NAME, JlinkTask) {
    mainClass = mainClassName
    mainModule = 'net.woggioni.gbcs.cli'
}

artifacts {
    release(envelopeJarTaskProvider)
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact envelopeJar
        }
    }
}


